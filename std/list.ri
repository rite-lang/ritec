import result:Result

/// Compute the length of a list.
pub fn length(xs: ['a]) -> int
  match xs
  | [_, ..xs] -> 1 + length(xs)
  | [] -> 0

/// Contatenate two lists.
pub fn concat(xs: ['a], ys: ['a]) -> ['a]
  match xs
  | [x, ..xs] -> [x, ..concat(xs, ys)]
  | [] -> ys

/// Reverse a list.
pub fn reverse(xs: ['a]) -> ['a]
  match xs
  | [x, ..xs] -> concat(reverse(xs), [x])
  | [] -> []

/// Map a function over a list.
pub fn map(f: fn('a) -> 'b, xs: ['a]) -> ['b]
  match xs
  | [x, ..xs] -> [f(x), ..map(f, xs)]
  | [] -> []

/// Try to map a function over a list.
/// If the function returns an error, the whole operation fails.
pub fn try_map(
  f: fn('a) -> Result<'b, 'c>
  xs: ['a]
) -> Result<['b], 'c>
  match xs
  | [x, ..xs] ->
    match f(x)
    | Err(e) -> Err(e)
    | Ok(y) -> try_map(f, xs) |> result:map(|ys| [y, ..ys])
  | [] -> Ok([])

/// Filter a list.
pub fn filter(f: fn('a) -> bool, xs: ['a]) -> ['a]
  match xs
  | [x, ..xs] ->
    match f(x)
    | true -> [x, ..filter(f, xs)]
    | false -> filter(f, xs)
  | [] -> []

/// Map a function over a list and filter out errors.
pub fn filter_map(
  f: fn('a) -> Result<'b, 'e>
  xs: ['a]
) -> ['b]
  match xs
  | [x, ..xs] ->
    match f(x)
    | Err(_) -> filter_map(f, xs)
    | Ok(y) -> [y, ..filter_map(f, xs)]
  | [] -> []
